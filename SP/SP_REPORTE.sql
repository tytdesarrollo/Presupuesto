USE [presama]
GO
/****** Object:  StoredProcedure [dbo].[SP_REPORTE]    Script Date: 12/10/2017 16:56:17 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[SP_REPORTE] 
(
	@IN_CODIGO_OPN VARCHAR(10),
	@IN_FECHA_INI DATE,
	@IN_FECHA_FIN DATE
)
AS
	DECLARE @TSQL NVARCHAR(MAX);   					      	-- SQL QUE TENDRA QUERY COMPLETO
	DECLARE @PARAMS NVARCHAR(MAX); 					   		-- PARAMETROS DEL QUERY
	DECLARE @COMPLEMENTO NVARCHAR(MAX) = 'FOR [MES] IN (';  -- COMPLEMENTO DE LOS MESES DEL QURY
	DECLARE @SELECTNULL NVARCHAR(MAX)= '';					-- CONVERTIR LOS CAMPOS NULL EN CERO
	DECLARE @CONTADOR INT = MONTH(@IN_FECHA_INI);			-- MES DONDE INICIA 
	DECLARE @CANTIDAD_TOTAL INT;
	DECLARE @CONTROLLER INT;

	-- CONTROLO LA VARIABLE @CONTROLLER SI ES IGUAL A CERO NO HAY DATOS Y TERMINA EL SP
	SET @CONTROLLER = 
		(SELECT CAST(COUNT(DISTINCT(DATENAME(MONTH,MPRFEC))) AS INT)
		FROM AUDI_ESM_PRESUP
		WHERE  MPRFEC BETWEEN @IN_FECHA_INI AND  @IN_FECHA_FIN
			AND OPNCOD = @IN_CODIGO_OPN);

	-- MENSAJE DE EXCEPCION CUANDO NO HAY DATOS
	IF @CONTROLLER = 0 	
		BEGIN	
			PRINT 'No hay datos entre el periodo '+convert(varchar,(@IN_FECHA_INI))+' hasta '+convert(varchar,(@IN_FECHA_FIN))
			RETURN
		END;
	ELSE	
		-- CANTIDAD DE MESES QUE SE VAN A RETORNAR
		SET @CANTIDAD_TOTAL = 
			(SELECT CAST(COUNT(DISTINCT(DATENAME(MONTH,MPRFEC))) AS INT)
			FROM AUDI_ESM_PRESUP
			WHERE  MPRFEC BETWEEN @IN_FECHA_INI AND  @IN_FECHA_FIN
				AND OPNCOD = @IN_CODIGO_OPN) + @CONTADOR;

		-- CICLO PARA LA CANTIDAD DE COLUMNAS
		WHILE (@CONTADOR < @CANTIDAD_TOTAL )
		BEGIN
			-- SE RELLENA EL COMPLEMENTO CON LA CANTIDAD DE MESES NECESARIA
			SET @COMPLEMENTO = @COMPLEMENTO + '['+CONVERT(VARCHAR,@CONTADOR)+']';
			--
			SET @SELECTNULL = @SELECTNULL + 'ISNULL(['+CONVERT(VARCHAR,@CONTADOR)+'], 0) AS '+DATENAME(MONTH,'01-'+CONVERT(VARCHAR,@CONTADOR)+'-2015')+'_'+CONVERT(VARCHAR,(YEAR(@IN_FECHA_INI)));

			--SI NO ES EL ULTIMO MES LE PONE COMA
			IF (@CONTADOR <> (@CANTIDAD_TOTAL -1))
			BEGIN
				--
				SET @COMPLEMENTO = @COMPLEMENTO + ','
				--
				SET @SELECTNULL = @SELECTNULL + ',';
			END		

			-- AUMENTA EL CONTADOR 1 UNIDAD
			SET @CONTADOR = @CONTADOR + 1;
		END
		-- CIERRA EL COMPLEMENTO
		SET @COMPLEMENTO = @COMPLEMENTO + ')';
	
		-- CREA EL QUERY
		SET @TSQL = N'
			SELECT '+@SELECTNULL+'
			FROM (
				SELECT  distinct(b.MES_FACTURA) MES,
				(select sum(a.mprval) from AUDI_ESM_PRESUP a where a.CODI_ESM = b.codi_esm and a.MES_FACTURA=b.MES_FACTURA) MPRVAL, CODI_ESM
				FROM AUDI_ESM_PRESUP b
				WHERE  b.MPRFEC between @IN_FECHA_INI and @IN_FECHA_FIN
				and b.OPNCOD = @IN_CODIGO_OPN
				--order by b.codi_esm
			) A
			PIVOT(
				MAX(MPRVAL)'+
				@COMPLEMENTO+
			')P';

		SET @PARAMS = N'@IN_CODIGO_OPN NVARCHAR(10),
						@IN_FECHA_INI DATE,
						@IN_FECHA_FIN DATE';

		EXECUTE sp_executesql @TSQL, @PARAMS, @IN_CODIGO_OPN = @IN_CODIGO_OPN, @IN_FECHA_INI = @IN_FECHA_INI, @IN_FECHA_FIN = @IN_FECHA_FIN;				
		--SELECT @CANTIDAD_TOTAL;	
